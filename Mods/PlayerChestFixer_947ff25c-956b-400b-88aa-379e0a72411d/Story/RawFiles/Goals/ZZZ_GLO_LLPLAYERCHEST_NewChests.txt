Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLPLAYERCHEST_InitReassignment();
LLPLAYERCHEST_InitFallbackPositions();
KBSECTION
PROC
LLPLAYERCHEST_InitReassignment()
THEN
SysClear("DB_LLPLAYERCHEST_PlayerChests_Chest", 2);
DB_LLPLAYERCHEST_PlayerChests_Chest((ITEMGUID)ITEMGUID_S_LVHub_PlayerChest_000_217e353f-6512-4808-aa11-3ec5cc06df49, (ITEMGUID)ITEMGUID_S_LVHub_FixedPlayerChest_01_77f1963d-d163-4856-b868-40380623c245);
DB_LLPLAYERCHEST_PlayerChests_Chest(ITEMGUID_S_LVHub_PlayerChest_001_18d2f084-cc6e-4890-878c-6c0e1ec3b1f2, ITEMGUID_S_LVHub_FixedPlayerChest_02_3c583ea9-a6a3-4d93-96d1-2c0cb2f58680);
DB_LLPLAYERCHEST_PlayerChests_Chest(ITEMGUID_S_LVHub_PlayerChest_002_df8ac858-679e-45d8-861a-6439980db2f4, ITEMGUID_S_LVHub_FixedPlayerChest_03_f74c6879-9319-41ec-92df-7e52ecd103d5);
DB_LLPLAYERCHEST_PlayerChests_Chest(ITEMGUID_S_LVHub_PlayerChest_003_2ef015b9-79cb-44dd-b895-ab58fe2c8d86, ITEMGUID_S_LVHub_FixedPlayerChest_04_af54b1f9-93e0-4767-96d4-75eedda69684);

PROC
LLPLAYERCHEST_InitReassignment()
THEN
SysClear("DB_GLO_LVHub_PlayerChests_Host", 1);
SysClear("DB_GLO_LVHub_PlayerChests_Chest", 1);
DB_GLO_LVHub_PlayerChests_Chest((ITEMGUID)ITEMGUID_S_LVHub_FixedPlayerChest_01_77f1963d-d163-4856-b868-40380623c245);
DB_GLO_LVHub_PlayerChests_Chest(ITEMGUID_S_LVHub_FixedPlayerChest_02_3c583ea9-a6a3-4d93-96d1-2c0cb2f58680);
DB_GLO_LVHub_PlayerChests_Chest(ITEMGUID_S_LVHub_FixedPlayerChest_03_f74c6879-9319-41ec-92df-7e52ecd103d5);
DB_GLO_LVHub_PlayerChests_Chest(ITEMGUID_S_LVHub_FixedPlayerChest_04_af54b1f9-93e0-4767-96d4-75eedda69684);
PROC_GLO_LVHub_PlayerChests_InitOnce();

PROC
LLPLAYERCHEST_InitFallbackPositions()
THEN
SysClear("DB_LLPLAYERCHEST_FallbackPosition", 8);
DB_LLPLAYERCHEST_FallbackPosition("LV_HoE_Main", (ITEMGUID)ITEMGUID_S_LVHub_PlayerChest_000_217e353f-6512-4808-aa11-3ec5cc06df49, 319.18112182617, 7.8018026351929, 580.04162597656, 0.0, -1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("LV_HoE_Main", ITEMGUID_S_LVHub_PlayerChest_001_18d2f084-cc6e-4890-878c-6c0e1ec3b1f2, 319.18112182617, 7.8018026351929, 577.78198242188, 0.0, -1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("LV_HoE_Main", ITEMGUID_S_LVHub_PlayerChest_002_df8ac858-679e-45d8-861a-6439980db2f4, 321.55151367188, 7.8018026351929, 577.34063720703, 0.0, 3.1415939331055, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("LV_HoE_Main", ITEMGUID_S_LVHub_PlayerChest_003_2ef015b9-79cb-44dd-b895-ab58fe2c8d86, 323.60034179688, 7.8018026351929, 577.34063720703, 0.0, 3.1415939331055, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("RC_Main", ITEMGUID_S_LVHub_PlayerChest_000_217e353f-6512-4808-aa11-3ec5cc06df49, 723.31298828125, 8.1076431274414, -39.099292755127, 0.0, -1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("RC_Main", ITEMGUID_S_LVHub_PlayerChest_001_18d2f084-cc6e-4890-878c-6c0e1ec3b1f2, 723.31298828125, 8.1076431274414, -41.358940124512, 0.0, -1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("RC_Main", ITEMGUID_S_LVHub_PlayerChest_002_df8ac858-679e-45d8-861a-6439980db2f4, 725.68334960938, 8.1076431274414, -41.800273895264, 0.0, 3.1415939331055, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("RC_Main", ITEMGUID_S_LVHub_PlayerChest_003_2ef015b9-79cb-44dd-b895-ab58fe2c8d86, 727.73217773438, 8.1076431274414, -41.800273895264, 0.0, 3.1415939331055, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("CoS_Main", ITEMGUID_S_LVHub_PlayerChest_000_217e353f-6512-4808-aa11-3ec5cc06df49, 226.49485778809, 16.092691421509, 266.87750244141, 0.0, 3.143360376358, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("CoS_Main", ITEMGUID_S_LVHub_PlayerChest_001_18d2f084-cc6e-4890-878c-6c0e1ec3b1f2, 228.75450134277, 16.092691421509, 266.87350463867, 0.0, 3.143360376358, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("CoS_Main", ITEMGUID_S_LVHub_PlayerChest_002_df8ac858-679e-45d8-861a-6439980db2f4, 229.20002746582, 16.092691421509, 269.24313354492, 0.0, 1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("CoS_Main", ITEMGUID_S_LVHub_PlayerChest_003_2ef015b9-79cb-44dd-b895-ab58fe2c8d86, 229.20364379883, 16.092691421509, 271.29193115234, 0.0, 1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("ARX_Main", ITEMGUID_S_LVHub_PlayerChest_000_217e353f-6512-4808-aa11-3ec5cc06df49, 383.45291137695, 21.018075942993, 804.75787353516, 0.0, -1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("ARX_Main", ITEMGUID_S_LVHub_PlayerChest_001_18d2f084-cc6e-4890-878c-6c0e1ec3b1f2, 383.45291137695, 21.018075942993, 802.49822998047, 0.0, -1.5707969665527, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("ARX_Main", ITEMGUID_S_LVHub_PlayerChest_002_df8ac858-679e-45d8-861a-6439980db2f4, 385.82330322266, 21.018075942993, 802.05688476563, 0.0, 3.1415939331055, 0.0);
DB_LLPLAYERCHEST_FallbackPosition("ARX_Main", ITEMGUID_S_LVHub_PlayerChest_003_2ef015b9-79cb-44dd-b895-ab58fe2c8d86, 387.87213134766, 21.018075942993, 802.05688476563, 0.0, 3.1415939331055, 0.0);

PROC
LLPLAYERCHEST_Updater_VersionChanged((STRING)_OldVersion, (STRING)_NewVersion)
AND
SysCount("DB_LLPLAYERCHEST_FallbackPosition", 8, 0)
THEN
LLPLAYERCHEST_InitFallbackPositions();

PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
DB_GLO_LVHub_PlayerChests_HostSecondary(_ProfileName, _Original)
THEN
NOT DB_GLO_LVHub_PlayerChests_HostSecondary(_ProfileName, _Original);
DB_GLO_LVHub_PlayerChests_HostSecondary(_ProfileName, _New);

PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
DB_GLO_LVHub_PlayerChests_Client(_ProfileName, _Original)
THEN
NOT DB_GLO_LVHub_PlayerChests_Client(_ProfileName, _Original);
DB_GLO_LVHub_PlayerChests_Client(_ProfileName, _New);

QRY
LLPLAYERCHEST_QRY_UseFallbackPosition((ITEMGUID)_Original, (ITEMGUID)_New, 0, (STRING)_Region)
THEN
DB_NOOP(1);

QRY
LLPLAYERCHEST_QRY_UseFallbackPosition((ITEMGUID)_Original, (ITEMGUID)_New, (INTEGER)_OriginalExists, (STRING)_Region)
AND
DB_LLPLAYERCHEST_FallbackPosition(_Region, _Original, _x, _y, _z, _Pitch, _Yaw, _Roll)
AND
GetDistanceToPosition(_Original, _x, _y, _z, _Dist)
AND
_Dist > 0.0
THEN
DB_NOOP(1);

PROC
LLPLAYERCHEST_TeleportChest((ITEMGUID)_Original, (ITEMGUID)_New, 1, (STRING)_Region)
THEN
SetOnStage(_Original, 0);

PROC
LLPLAYERCHEST_TeleportChest((ITEMGUID)_Original, (ITEMGUID)_New, (INTEGER)_OriginalExists, (STRING)_Region)
AND
NOT DB_LLPLAYERCHEST_Temp_ChestTeleported(_New, _)
AND
NOT LLPLAYERCHEST_QRY_UseFallbackPosition(_Original, _New, _OriginalExists, _Region)
AND
GetPosition(_Original, _x, _y, _z)
AND
GetRotation(_Original, _PitchDegree, _YawDegree, _RollDegree)
AND
RealProduct(_PitchDegree, 0.0174533, _Pitch)
AND
RealProduct(_YawDegree, 0.0174533, _Yaw)
AND
RealProduct(_RollDegree, 0.0174533, _Roll)
THEN
ItemToTransform(_New, _x, _y, _z, _Pitch, _Yaw, _Roll, 1, NULL_00000000-0000-0000-0000-000000000000);
DB_LLPLAYERCHEST_Temp_ChestTeleported(_New, _Original);

PROC
LLPLAYERCHEST_TeleportChest((ITEMGUID)_Original, (ITEMGUID)_New, (INTEGER)_OriginalExists, (STRING)_Region)
AND
NOT DB_LLPLAYERCHEST_Temp_ChestTeleported(_New, _)
AND
LLPLAYERCHEST_QRY_UseFallbackPosition(_Original, _New, _OriginalExists, _Region)
AND
DB_LLPLAYERCHEST_FallbackPosition(_Region, _Original, _x, _y, _z, _Pitch, _Yaw, _Roll)
THEN
ItemToTransform(_New, _x, _y, _z, _Pitch, _Yaw, _Roll, 1, NULL_00000000-0000-0000-0000-000000000000);

PROC
LLPLAYERCHEST_TeleportChest((ITEMGUID)_Original, (ITEMGUID)_New, (INTEGER)_OriginalExists, (STRING)_Region)
AND
DB_LLPLAYERCHEST_Temp_ChestTeleported(_New, _Original)
THEN
NOT DB_LLPLAYERCHEST_Temp_ChestTeleported(_New, _Original);

PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
ObjectExists(_Original, _OriginalExists)
THEN
LLPLAYERCHEST_TeleportChest(_Original, _New, _OriginalExists, _Region);
LLPLAYERCHEST_OnChestTeleported(_Original, _New, _OriginalExists);

PROC
LLPLAYERCHEST_OnChestTeleported((ITEMGUID)_Original, (ITEMGUID)_New, 1)
THEN
MoveAllItemsTo(_Original, _New);

IF
StoryEvent((ITEMGUID)_Item, "LLPLAYERCHEST_Iterator_MoveToNew")
AND
ObjectExists(_Item, 1)
AND
ItemIsDestroyed(_Item, 0)
AND
GetInventoryOwner(_Item, (ITEMGUID)_Original)
AND
DB_LLPLAYERCHEST_PlayerChests_Chest(_Original, _New)
AND
ItemGetAmount(_Item, _Amount)
THEN
ItemToInventory(_Item, _New, _Amount, 0, 0);

/*
PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
DB_CurrentLevel(_Level)
THEN
NRD_LuaCall("LLPLAYERCHEST_Ext_LogPosition", "DB_LLPLAYERCHEST_FallbackPosition", _Level, (STRING)_Original);
*/

QRY
LLPLAYERCHEST_QRY_ChestShouldBeOnStage((ITEMGUID)_Chest)
AND
DB_GLO_LVHub_PlayerChests_Client(_ProfileName, _Chest)
AND
DB_IsPlayer(_Player)
AND
CharacterGetReservedUserID(_Player, _UserID)
AND
GetUserProfileID(_UserID, _ProfileName)
THEN
DB_NOOP(1);

QRY
LLPLAYERCHEST_QRY_ChestShouldBeOnStage((ITEMGUID)_Chest)
AND
DB_GLO_LVHub_PlayerChests_Host(_Chest)
THEN
DB_NOOP(1);

QRY
LLPLAYERCHEST_QRY_ChestShouldBeOnStage((ITEMGUID)_Chest)
AND
NOT DB_GLO_LVHub_PlayerChests_Client(_, _Chest)
AND
NOT DB_GLO_LVHub_PlayerChests_Host(_Chest)
AND
ContainerGetGoldValue(_Chest, _GoldValue)
AND
_GoldValue > 0
THEN
DB_NOOP(1);

PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
ObjectIsOnStage(_New, 0)
AND
LLPLAYERCHEST_QRY_ChestShouldBeOnStage(_New)
THEN
DB_LLPLAYERCHEST_PlayerChests_Temp_ChestUpdated(_New);
SetOnStage(_New, 1);

PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
NOT DB_LLPLAYERCHEST_PlayerChests_Temp_ChestUpdated(_New)
AND
ObjectIsOnStage(_New, 1)
AND
NOT LLPLAYERCHEST_QRY_ChestShouldBeOnStage(_New)
THEN
SetOnStage(_New, 0);

PROC
LLPLAYERCHEST_UpdateChest((ITEMGUID)_Original, (ITEMGUID)_New, (STRING)_Region)
AND
DB_LLPLAYERCHEST_PlayerChests_Temp_ChestUpdated(_New)
THEN
NOT DB_LLPLAYERCHEST_PlayerChests_Temp_ChestUpdated(_New);

QRY
LLPLAYERCHEST_QRY_ChestNeedsUpdating((ITEMGUID)_Original, (ITEMGUID)_New, 0, (STRING)_Region)
AND
LLPLAYERCHEST_QRY_ChestShouldBeOnStage(_New)
THEN
DB_NOOP(1);

QRY
LLPLAYERCHEST_QRY_ChestNeedsUpdating((ITEMGUID)_Original, (ITEMGUID)_New, 1, (STRING)_Region)
AND
NOT LLPLAYERCHEST_QRY_ChestShouldBeOnStage(_New)
THEN
DB_NOOP(1);

QRY
LLPLAYERCHEST_QRY_ChestNeedsUpdating((ITEMGUID)_Original, (ITEMGUID)_New, (INTEGER)_NewOnStage, (STRING)_Region)
AND
DB_LLPLAYERCHEST_FallbackPosition(_Region, _Original, _x, _y, _z, _Pitch, _Yaw, _Roll)
AND
GetDistanceToPosition(_New, _x, _y, _z, _Dist)
AND
_Dist > 0.0
THEN
DB_NOOP(1);

QRY
LLPLAYERCHEST_QRY_ChestNeedsUpdating((ITEMGUID)_Original, (ITEMGUID)_New, (INTEGER)_NewOnStage, (STRING)_Region)
AND
ObjectExists(_Original, 1)
AND
ObjectIsOnStage(_Original, 1)
THEN
SetOnStage(_Original, 0);

//REGION UPDATING_CHESTS
//In existing saves, if a chest is on/offstage when it shouldn't be, clear DB_LLPLAYERCHEST_InitializedRegion for this region.
IF
SavegameLoading(_,_,_,_)
AND
DB_CurrentLevel(_Region)
AND
DB_LLPLAYERCHEST_InitializedRegion(_Region)
AND
DB_LLPLAYERCHEST_PlayerChests_Chest(_Original, _New)
AND
ObjectIsOnStage(_New, _OnStage)
AND
LLPLAYERCHEST_QRY_ChestNeedsUpdating(_Original, _New, _OnStage, _Region)
THEN
NOT DB_LLPLAYERCHEST_InitializedRegion(_Region);

IF
GameStarted(_Region, _)
AND
NOT DB_LLPLAYERCHEST_InitializedRegion(_Region)
AND
DB_CheckLevelStart_PlayerChests(_Region)
THEN
DB_LLPLAYERCHEST_InitializedRegion(_Region);

IF
DB_LLPLAYERCHEST_InitializedRegion(_Region)
AND
DB_LLPLAYERCHEST_PlayerChests_Chest(_Original, _New)
AND
ObjectExists(_New, 1)
THEN
LLPLAYERCHEST_UpdateChest(_Original, _New, _Region);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "GLO_LVHub_LV_HoE_Main"